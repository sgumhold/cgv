#version 430

//***** begin interface of fragment2d.glfs ***********************************
uniform vec4 border_color = vec4(1.0);
uniform float border_width = 0.0;

vec2 get_shape_size();
vec4 get_color();
void override_color(in vec4 color);
void finish_sdf_fragment2d(in float signed_distance, in vec2 texcoords);
void finish_sdf_fragment2d(in float signed_distance);
void finish_fragment2d(in vec2 texcoords);
void finish_fragment2d();
//***** end interface of fragment2d.glfs ***********************************

//uniform float dash_length = 0.0;
uniform float width = 1.0;
uniform float feather_width = 1.0;

in vec4 color_fs;
in flat vec2 size_fs;
in vec2 tex_coord_fs;
in vec2 pix_coord_fs;
in flat vec2 pos_a_fs;
in flat vec2 pos_b_fs;
in flat vec2 pos_c_fs;

out vec4 frag_out;

float dot2(in vec2 v) {
	return dot(v,v);
}

float cross2(in vec2 a, in vec2 b) {
	return a.x*b.y - a.y*b.x;
}

// https://www.shadertoy.com/view/MlKcDD
float ud_bezier(in vec2 pos, in vec2 A, in vec2 B, in vec2 C)
{    
    vec2 a = B - A;
    vec2 b = A - 2.0*B + C;
    vec2 c = a * 2.0;
    vec2 d = A - pos;

    float kk = 1.0/dot(b,b);
    float kx = kk * dot(a,b);
    float ky = kk * (2.0*dot(a,a)+dot(d,b))/3.0;
    float kz = kk * dot(d,a);      

    float res = 0.0;

    float p = ky - kx*kx;
    float p3 = p*p*p;
    float q = kx*(2.0*kx*kx - 3.0*ky) + kz;
    float h = q*q + 4.0*p3;

    if( h>=0.0 ) 
    {   // 1 root
        h = sqrt(h);
        vec2 x = (vec2(h,-h)-q)/2.0;
        vec2 uv = sign(x)*pow(abs(x), vec2(1.0/3.0));
        float t = clamp( uv.x+uv.y-kx, 0.0, 1.0 );
        res = dot2(d+(c+b*t)*t);
    }
    else 
    {   // 3 roots
        float z = sqrt(-p);
        float v = acos(q/(p*z*2.0))/3.0;
        float m = cos(v);
        float n = sin(v)*1.732050808;
        vec3  t = clamp( vec3(m+m,-n-m,n-m)*z-kx, 0.0, 1.0 );
        res = min( dot2(d+(c+b*t.x)*t.x),
                   dot2(d+(c+b*t.y)*t.y) );
        // the third root cannot be the closest. See https://www.shadertoy.com/view/4dsfRS
        // res = min(res,dot2(d+(c+b*t.z)*t.z));
    }
    
    return sqrt(res);
}

void main() {
	vec4 color = color_fs;

	float s;
	float dist = ud_bezier(pix_coord_fs, pos_a_fs, pos_b_fs, pos_c_fs);

	float limit = 0.5*width - 0.5*feather_width;
	float inv_feather = 1.0 / feather_width;

	float t = clamp((dist - limit) * inv_feather, 0.0, 1.0);

	if(border_width > 0.0) {
		float border_limit = 0.5*width - border_width - 0.5*feather_width;
		float tb = clamp((dist - border_limit) * inv_feather, 0.0, 1.0);
		color = mix(color, border_color, tb);
	}

	color.a *= 1.0 - t;

	override_color(color);
	finish_fragment2d();
}
