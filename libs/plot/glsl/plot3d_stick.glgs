#version 430 core

layout(points) in;
layout(triangle_strip, max_vertices = 28) out;

in STICK_3D_GS {
	vec4 start_sphere;
	vec4 end_sphere;
	vec4 start_color;
	vec4 end_color;
} gi[];

out vec3 position;
out flat vec4 start_fs;
out flat vec4 end_fs;
out flat vec4 tp2;
out flat vec4 tp3;
out flat vec4 color_start_fs;
out flat vec4 color_end_fs;

vec3 ortho_vec(vec3 v) {

	return abs(v.x) > abs(v.z) ? vec3(-v.y, v.x, 0.0f) : vec3(0.0f, -v.z, v.y);
}

//***** begin interface of view.glsl ***********************************
mat4 get_modelview_matrix();
mat4 get_projection_matrix();
mat4 get_modelview_projection_matrix();
mat4 get_inverse_modelview_matrix();
mat4 get_inverse_modelview_projection_matrix();
mat3 get_normal_matrix();
mat3 get_inverse_normal_matrix();
//***** end interface of view.glsl ***********************************

//***** begin interface of plot_lib.glsl ***********************************
uniform float attribute_min[8];
uniform float attribute_max[8];
float tick_space_from_attribute_space(int ai, float value);
float attribute_space_from_tick_space(int ai, float value);
float window_space_from_tick_space(int ai, float value);
float tick_space_from_window_space(int ai, float value);
vec3 plot_space_from_window_space(vec3 pnt);
vec3 window_space_from_plot_space(vec3 pnt);
vec3 world_space_from_plot_space(vec3 pnt);
vec3 map_color(in float v_window, int idx);
vec3 map_color(in float attributes[8], in vec3 base_color, int idx);
float map_opacity(in float v_window, int idx);
float map_opacity(in float attributes[8], in float base_opacity, int idx);
float map_size(in float v_window, int idx);
float map_size(in float attributes[8], in float base_size, int idx);
vec3 map_color(in float v_window);
vec3 map_color(in float attributes[8], in vec3 base_color);
float map_opacity(in float v_window);
float map_opacity(in float attributes[8], in float base_opacity);
float map_size(in float v_window);
float map_size(in float attributes[8], in float base_size);
//***** end interface of plot_lib.glsl ***********************************


void main()
{
	vec4 start = gi[0].start_sphere;
	vec4 end = gi[0].end_sphere;

	float l = length(end.xyz - start.xyz);
	vec3 delta = (end.xyz - start.xyz) / l;

	float max_d = 2 * max(start.w, end.w);

	mat4 p = get_projection_matrix();
	tp2 = vec4(p[0][2], p[1][2], p[2][2], p[3][2]);
	tp3 = vec4(p[0][3], p[1][3], p[2][3], p[3][3]);

	vec3 center = start.xyz + end.xyz + delta*(end.w - start.w);
	center *= 0.5;

	// create transformation matrix as modelview * translation * rotation * scale
	// rotation part
	mat4 R = mat4(1.0);
	R[0] = vec4(delta, 0.0);
	R[1] = vec4(normalize(ortho_vec(delta)), 0.0);
	R[2] = vec4(cross(delta, R[1].xyz), 0.0);
	R[3] = vec4(center, 1.0); // translation part

	// scale part
	R[0] *= l + start.w + end.w;
	R[1] *= max_d;
	R[2] *= max_d;

	// transform to eye space
	mat4 M = get_modelview_matrix() * R;
	
	// transform cone start and end position to eye space
	start_fs = start;
	end_fs = end;

	color_start_fs = gi[0].start_color;
	color_end_fs = gi[0].end_color;

	// generate transformed base vector corners of the box
	vec4 C0 = M * vec4(-0.5, -0.5, -0.5, 1.0);
	vec4 C1 = M * vec4(0.5, -0.5, -0.5, 1.0);
	vec4 C2 = M * vec4(-0.5, 0.5, -0.5, 1.0);
	vec4 C4 = M * vec4(-0.5, -0.5, 0.5, 1.0);

	// calculate remaining corners
	vec4 D20 = C2 - C0;

	vec4 C3 = C1 + D20;
	vec4 C5 = C1 + C4-C0;
	vec4 C6 = C4 + D20;
	vec4 C7 = C5 + D20;
	
	mat4 PM = get_projection_matrix();

	position = C6.xyz;
	gl_Position = PM * C6;
	EmitVertex();

	position = C7.xyz;
	gl_Position = PM * C7;
	EmitVertex();

	position = C4.xyz;
	gl_Position = PM * C4;
	EmitVertex();

	position = C5.xyz;
	gl_Position = PM * C5;
	EmitVertex();

	position = C1.xyz;
	gl_Position = PM * C1;
	EmitVertex();

	position = C7.xyz;
	gl_Position = PM * C7;
	EmitVertex();
	
	position = C3.xyz;
	gl_Position = PM * C3;
	EmitVertex();
	
	position = C6.xyz;
	gl_Position = PM * C6;
	EmitVertex();
	
	position = C2.xyz;
	gl_Position = PM * C2;
	EmitVertex();
	
	position = C4.xyz;
	gl_Position = PM * C4;
	EmitVertex();
	
	position = C0.xyz;
	gl_Position = PM * C0;
	EmitVertex();
	
	position = C1.xyz;
	gl_Position = PM * C1;
	EmitVertex();
	
	position = C2.xyz;
	gl_Position = PM * C2;
	EmitVertex();
	
	position = C3.xyz;
	gl_Position = PM * C3;
	EmitVertex();

	EndPrimitive();
}